plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id "io.gitlab.arturbosch.detekt" version "1.22.0"
    id 'org.jetbrains.dokka'
    id 'app.cash.paparazzi' version '1.2.0'
}

group 'com.togitech'
version '2.0.1'

kotlin {
    jvmToolchain(11)
}

android {
    compileSdk 33
    namespace = 'com.togitech.ccp'

    defaultConfig {
        minSdk 24
        targetSdk 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        compose true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    composeOptions {
        // https://androidx.dev/storage/compose-compiler/repository/
        kotlinCompilerExtensionVersion "$compose_compiler_version"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    publishing {
        singleVariant('release') {
            withJavadocJar()
        }
    }
}

dependencies {
    api "org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.5"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.activity:activity-compose:$activity_compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    api 'com.googlecode.libphonenumber:libphonenumber:8.13.8'
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    detektPlugins("ru.kode:detekt-rules-compose:1.2.2")
    detektPlugins("com.twitter.compose.rules:detekt:0.0.26")
}

detekt {
    config = files(project.rootProject.file("detekt.yml"))
    allRules = true
}

afterEvaluate {
    publishing {
        publications( {
            release(MavenPublication) {
                from components.release
                groupId = 'com.togisoft'
                artifactId = 'jetpack_country_code_picker'
                version = '2.0.1'
            }
        })
    }
}